#!/usr/bin/perl

use warnings;
use strict;
no indirect;
no autovivification;

# Converted to a real package number by autotools.
my $VERSION_NUMBER = "~~PACKAGE-VERSION~~";

#========================================================================#

=pod

=head1 NAME

sum2table - render one or two DeJaGnu sum files as a table

=head1 OPTIONS

B<sum2table> [-h|--help] [--version]
             [--sort=SPEC]
             [--filter=PATTERN]
             [--only-changed|-c]
             SUM_FILE_1 [ SUM_FILE_2 ]

=head1 SYNOPSIS

With one summary file, write the file as a table.  Each row has the name of
a testfile (like gdb.base/break.exp) followed by the number of passes,
failures, etc.

When passed two summary files, the first is used as a benchmark level, the
second summary file in then superimposed as an increment or decrement on
the initial benchmark value.

Lets, imagine running a test file 'tool/test.exp', the results might look
like this when only one summary file is passed:

  |               | FAIL | PASS |
  -------------------------------
  | tool/test.exp |    3 |    5 |

Then, when we pass two summary files the table might change to this:

  |               |      FAIL |      PASS |
  -----------------------------------------
  | tool/test.exp |    3 (+2) |    5 (-2) |

This indicates that in the second summary file we have 2 less passes and 2
extra failures.

The columns are ordered so that "interesting" (as in columns that indicate
bad changes) are placed first, the column ordering is:

  FAIL UNRESOLVED UNSUPPORTED UNTESTED PASS XFAIL KFAIL XPASS KPASS

Only columns that contain a result are displayed.

By default the results are sorted by the name of the test file name,
however, the I<--sort> command line option allows sorting to be performed
based on other columns.  The --sort option takes a SPEC, the SPEC is made
from two parts, a column name and a sort type.

Valid column names are things like PASS, FAIL, UNSUPPORTED, etc.

The sort type is one of 'new', 'old', or 'diff'.  The 'new' type sorts
tests with the highest new value to the top, 'old' sorts the highest old
value to the top, and finally, 'diff' sorts based on the difference between
the new and old values.

An example of a SPEC would be 'unresolved:diff', so tests with the biggest
difference in the number of unresolved tests will be sorted to the top of
the table.

It is possible to list many sort specifiers, separating them with a comma,
the table rows are sorted based on each of the specifiers in turn.  So
this:

  fail:diff,unresolved:diff

Will sort the table rows so that rows with the highest 'diff' in the fail
column are first, followed by rows with the highest 'diff' in the
unresolved column.

The 'diff' type is the default, so the previous example can be shortened to
this:

  fail,unresolved

If the magic value 'BAD' is used for SPEC then this expands to:

  fail:diff,unresolved:diff,unsupported:diff,untested:diff

if two filenames are given, which will list all of the tests that have
gotten worse at the top.  Alternatively, if 'BAD' is used for SPEC and only
one filename is given, then this expands to:

  fail:new,unresolved:new,unsupported:new,untested:new

which will list all of the tests with failures at the top.

When performing a comparison between two summary files, testnames might
have '(+)' or '(-)' appended to them.  The '(+)' indicates a test that only
appears in the second summary file, while a '(-)' indicates a test that is
present in the first, but is missing from the second.

The I<--filter> option allows the user to filter which test results are
printed.  The filter option can be given multiple times, and test name that
matches any of the filters will be displayed, all non matching testnames
are removed from the table.  The value passed to the filter is treated as a
regular expression.

The I<--only-changed> option (or I<-c> for short) filters out all lines
from the results table that have not changed, keeping only those lines that
have some changes.  This option only makes sense when two summary files are
being compared, and will cause an error if it is used with only one summary
file.  Further, this option can be combined with the --filter option, lines
will only be displayed if they match a filter AND they have changed.

When results are filtered using either --filter or --only-changed then
sorting the results with the --sort argument still works.

=cut

#========================================================================#

use FindBin;
use lib "$FindBin::Bin/lib";
use GiveHelp qw/usage/;         # Allow -h or --help command line options.
use boolean;
use List::Util qw/max/;
use List::MoreUtils qw/uniq/;
use Getopt::Long;
use Carp::Assert;
use RunTestUtils::SumFile;
use RunTestUtils::FilterManager;

my $SYSCONFDIR = "$FindBin::Bin/etc";
my $FILTERS_PATH = "$SYSCONFDIR/runtest-utils/filters/";

#========================================================================#

my @all_possible_status_types = qw/FAIL UNRESOLVED UNSUPPORTED UNTESTED
                                   PASS XFAIL KFAIL XPASS KPASS/;

#========================================================================#

exit (main ());

#========================================================================#

=pod

=head1 METHODS

The following methods are defined in this script.

=over 4

=cut

#========================================================================#

=pod

=item B<results_contain_testname>

Takes a result set and the name of a test.  Returns true if there are
results for that testname in the result set, otherwise it returns false.

=cut

sub results_contain_testname {
  my $results = shift;
  my $testname = shift;

  return (exists ($results->{-by_testname}->{$testname}));
}

#========================================================================#

=pod

=item B<fill_in_sort_indexes>

Takes the sort specification list as computed by I<build_sort_spec> and
adds an extra field to each hash-reference in the list that is the column
index for that field.  The index will depend on the order of the columns in
the results table, which can change as we remove columns that are all 0.

Pre-computing the index speeds up the later sorting of the results.

=cut

sub fill_in_sort_indexes {
  my $sort_spec = shift;
  my $status_types = shift;

  foreach my $sort (@{$sort_spec})
  {
    # As we only display the columns that will actuall have results in
    # them, so we need to trim out sort specifiers that are not needed, and
    # also figure out the column indexes for the things we will sort on.
    my $found = false;
    my $index = 1;
    foreach (@{$status_types})
    {
      if ($_ eq $sort->{-field})
      {
        $found = true;
        last;
      }
      $index++;
    }
    # Set -index to be the index of the column we'll be sorting on.  Start
    # at 1 as the first column (column 0 of the table holds the name of the
    # test file).  If the column is not found in the table then set the
    # index to undef, we'll filter the sort list below.
    $sort->{-index} = $found ? $index : undef;
  }

  # Trim out columns where the index is undef, these are columns that will
  # not appear in our result table.  We need to be careful here not to
  # change the array reference that SORT_SPEC points to, as SORT_SPEC is
  # passed by value into this function, and we want our changes to the
  # referenced array to be seen in the caller.
  @{$sort_spec} = grep { defined ($_->{-index}) } @{$sort_spec};
}

#========================================================================#

=pod

=item B<build_sort_spec>

Takes a string that is the sort specification as passed to --sort command
line argument (or the default if the user didn't pass anything) and returns
a list of hash-references which we use for sorting.

=cut

sub build_sort_spec {
  my $spec_string = shift;

  my @spec = ();
  foreach my $spec (split /,/, $spec_string)
  {
    my ($field, $type) = split /:/, $spec;
    my $found = false;

    (defined $field)
      or die "Missing sort field from --sort=$spec_string\n";

    foreach (@all_possible_status_types)
    {
      if (uc ($field) eq $_)
      {
        $found = true;
        last;
      }
    }
    ($found) or
      die "Unknown sort field '$field' in $spec_string\n";
    $field = uc ($field);

    (defined $type) or $type = "diff";
    $found = false;
    foreach (qw/new old diff/)
    {
      if (lc($type) eq $_)
      {
        $found = true;
        last;
      }
    }
    ($found) or
      die "Unknown sort type '$type' in $spec_string";
    $type = lc ($type);

    my $sort = { -field => $field,
                 -type => $type };
    push @spec, $sort;
  }

  return \@spec;
}

#========================================================================#

=pod

=item B<compare_rows>

Takes two rows from the results table as the first two parameters, and a
reference to a list of sort specifications as the third parameter.  Return
-1, 0, +1 as a result based on the required sort ordering as defined by the
sort specifications.

Each sort specification is a hash with the keys I<-index> and I<-type>, the
type is a string I<old>, I<new>, or I<diff>.  The index is the index into
the row of the table (which should not be 0).

=cut

sub compare_rows {
  my $row_a = shift;
  my $row_b = shift;
  my $spec_list = shift;

  assert (ref ($spec_list) eq 'ARRAY');

  if (scalar(@{$spec_list}) == 0)
  {
    # No compare spec left, sort by testname.
    return ($row_a->[0]->{__string__}
              cmp $row_b->[0]->{__string__});
  }

  # OK, we have a compare spec available, grab it, but first, copy the
  # compare spec array so we're not corrupting the parents value.
  my @all_spec = @{$spec_list};
  my $spec = shift @all_spec;
  my $cmp = 0;
  my $index = $spec->{-index};

  assert ($index != 0);

  if ($spec->{-type} eq "old")
  {
    $cmp = $row_b->[$index]->{__old_value__}
      <=> $row_a->[$index]->{__old_value__};
  }
  elsif ($spec->{-type} eq "new")
  {
    $cmp = $row_b->[$index]->{__new_value__}
      <=> $row_a->[$index]->{__new_value__};
  }
  elsif ($spec->{-type} eq "diff")
  {
    $cmp = ($b->[$index]->{__new_value__}
              - $row_b->[$index]->{__old_value__})
      <=> ($row_a->[$index]->{__new_value__}
           - $row_a->[$index]->{__old_value__});
  }

  if ($cmp == 0)
  {
    $cmp = compare_rows ($row_a, $row_b, \@all_spec);
  }

  return $cmp;
}

#========================================================================#

=pod

=item B<build_table_columns>

Builds all of the columns that form a single row of the results table.
Takes the name of a test script, two result sets, and a list of the
statuses that reflect the order of the columns in the table.

The second result set can be undef if we're only processing a single
summary file.

This function returns a list of hash-references.  Each hash reference
represents a single column.

=cut

sub build_table_columns {
  my $testname = shift;
  my $results_1 = shift;
  my $results_2 = shift;
  my $status_types = shift;
  my $test_set_changed = shift;

  my @columns = ();
  my $suffix = "";
  if (defined ($results_2))
  {
    if (not (results_contain_testname ($results_1, $testname)))
    {
      assert ($test_set_changed);
      $suffix = " (+)";
    }
    elsif (not (results_contain_testname ($results_2, $testname)))
    {
      assert ($test_set_changed);
      $suffix = " (-)";
    }
    elsif ($test_set_changed)
    {
      $suffix = "    ";
    }
  }
  push @columns, { __string__ => $testname.$suffix };

  foreach (@{$status_types})
  {
    my $val = $results_1->{-by_testname}->{$testname}->{$_};
    $val = 0 unless (defined ($val));

    my $other = $results_2->{-by_testname}->{$testname}->{$_};
    $other = $val unless (defined ($other));

    my $str = "".$val;
    if ($other != $val)
    {
      $str .= " (";
      if ($other > $val)
      {
        $str .= "+".($other - $val);
      }
      else
      {
        $str .= "-".($val - $other);
      }
      $str .= ")";
    }
    push @columns, { __string__ => $str,
                     __old_value__ => $val,
                     __new_value__ => $other };
  }

  return @columns;
}

#========================================================================#

=pod

=item B<process_file>

Takes the name of a file to load, and returns a hash containing the results
loaded from the file.

If the filename passed in is undefined, then a valid results hash is still
returned, it will just contain no results.

If the file fails to load for any other reason then the function will die.

=cut

sub process_file {
  my $filename = shift;

  my $results = { -by_testname => {},
                  -status_types => {} };
  return $results if (not (defined ($filename)));

  (-f $filename) and (-r $filename)
    or die "File '$filename' is not a readable file\n";

  my $summary_file = RunTestUtils::SumFile->parse ($filename);
  my @results = $summary_file->results ();

  foreach my $r (@results)
  {
    my $testname = $r->get_path ();
    my $status = $r->get_status ();

    $results->{-by_testname}->{$testname} = {}
      unless (results_contain_testname ($results, $testname));
    $results->{-by_testname}->{$testname}->{$status} = 0
      unless (exists ($results->{-by_testname}->{$testname}->{$status}));
    $results->{-by_testname}->{$testname}->{$status}++;
    $results->{-status_types}->{$status} = true;
  }

  return $results;
}

#========================================================================#

=pod

=item B<main>

Main function.  Called with no argument, returns exit status.

=cut

sub main {
  my @filters = ();
  my $sort = undef;
  my $only_changed = false;
  my $show_version = false;

  GetOptions ("version" => \$show_version,
              "sort=s" => \$sort,
              "filter=s" => \@filters,
              "only-changed|c" => \$only_changed);

  if ($show_version)
  {
    print "$FindBin::Script version $VERSION_NUMBER\n";
    exit (0);
  }

  my $filename = shift @ARGV;
  (defined $filename) or usage ();
  my $filename_2 = shift @ARGV;

  if ($only_changed and (not (defined ($filename_2))))
  {
    die ("Error: The --only-changed option can only be used with ".
           "two summary files\n");
  }

  if (defined ($sort))
  {
    if ($sort eq "BAD")
    {
      if (defined ($filename_2))
      {
        $sort = "fail:diff,unresolved:diff,unsupported:diff,untested:diff";
      }
      else
      {
        $sort = "fail:new,unresolved:new,unsupported:new,untested:new";
      }
    }
    $sort = build_sort_spec ($sort);
  }

  RunTestUtils::FilterManager::load_filters ($FILTERS_PATH);

  my $results_1 = process_file ($filename);
  my $results_2 = undef;
  $results_2 = process_file ($filename_2) if (defined $filename_2);

  my @status_types = ();
  foreach (@all_possible_status_types)
  {
    if (exists ($results_1->{-status_types}->{$_})
          or exists ($results_2->{-status_types}->{$_}))
    {
      push @status_types, $_;
    }
  }

  if (defined ($sort))
  {
    fill_in_sort_indexes ($sort, \@status_types);
  }

  # Build sorted list of all test names for which we will display results.
  my @all_test_names = (keys (%{$results_1->{-by_testname}}),
                        keys (%{$results_2->{-by_testname}}));
  @all_test_names = sort (@all_test_names);
  @all_test_names = uniq (@all_test_names);

  # Filter the list of test names based on the --filter command line
  # option(s), or if --only-changed was passed.
  if ((scalar (@filters) > 0) or $only_changed)
  {
    my @r;
    my $init_found_val = not (scalar (@filters) > 0);

    @filters = map { qr/$_/ } @filters;

    foreach my $n (@all_test_names)
    {
      # If we have some filters then we initiaise FOUND to false, otherwise
      # it will be initiaised to true.
      my $found = $init_found_val;

      # Does the testname match any filter?  If it does then FOUND will be
      # changed to true, otherwise, FOUND is left unchanged.
      foreach my $re (@filters)
      {
        if ($n =~ $re)
        {
          $found = true;
          last;
        }
      }

      # If the user is only interested in changed results, and FOUND is
      # otherwise true (so we would show this result) then check to see if
      # the result has changed.
      if ($only_changed and $found)
      {
        $found = false;

        if (results_contain_testname ($results_1, $n)
              != results_contain_testname ($results_2, $n))
        {
          $found = true;
        }
        else
        {
          assert (results_contain_testname ($results_1, $n));
          assert (results_contain_testname ($results_2, $n));

          foreach my $status (@status_types)
          {
            my $old_val = 0;
            my $new_val = 0;

            # Use exists on each step in the chain to avoid
            # autovivification.
            if (exists ($results_1->{-by_testname}->{$n}->{$status}))
            {
              $old_val = $results_1->{-by_testname}->{$n}->{$status};
            }

            # Use exists on each step in the chain to avoid
            # autovivification.
            if (exists ($results_2->{-by_testname}->{$n}->{$status}))
            {
              $new_val = $results_2->{-by_testname}->{$n}->{$status};
            }

            if ($old_val != $new_val)
            {
              $found = true;
              last;
            }
          }
        }
      }

      push @r, $n if ($found);
    }

    @all_test_names = @r;
  }

  my $test_set_changed = false;
  foreach my $n (@all_test_names)
  {
    if (results_contain_testname ($results_1, $n)
          != results_contain_testname ($results_2, $n))
    {
      $test_set_changed = true;
      last;
    }
  }

  # Initialise the list of widths of each columns.
  my @widths = (0);
  foreach (@status_types)
  {
    push @widths, length ($_);
  }

  # Build up the rows of the table, and finish computing the widths of all
  # of the columns.
  my @rows;
  foreach my $testname (@all_test_names)
  {
    my @columns = build_table_columns ($testname, $results_1,
                                       $results_2, \@status_types,
                                       $test_set_changed);
    push @rows, \@columns;

    for (my $i = 0; $i < scalar (@columns); $i++)
    {
      if (length ($columns[$i]->{__string__}) > $widths[$i])
      {
        $widths[$i] = length ($columns[$i]->{__string__});
      }
    }
  }

  # Sort the rows of the table based on any --sort command line option.
  if (defined ($sort))
  {
    @rows = sort { compare_rows ($a, $b, $sort) } @rows;
  }

  # Build up the printf format string, and print the rows of the table.
  my $format = "|";
  my $div_length = 1;

  foreach (@widths)
  {
    $format .= " %".$_."s |";
    $div_length += 3 + $_;
  }
  $format .= "\n";

  my @params = ("");
  foreach (@status_types)
  {
    push @params, $_;
  }

  printf $format, @params;
  print "-"x$div_length."\n";

  foreach my $row (@rows)
  {
    printf $format, map { $_->{__string__} } @{$row};
  }

  return 0;
}

#========================================================================#

=pod

=back

=head1 AUTHOR

Andrew Burgess, 04 Jun 2017

=cut
